#example mission box2.flo
# with max depth protection

house box2

init max.depth with 50.0

framer vehiclesim be active first vehicle_run

frame vehicle_run
   do simulator motion uuv

framer mission be active first northleg

frame depthmax
   go abort if state.depth >= .max.depth

frame northleg in depthmax
   set elapsed with 20.0
   set goal.heading with 0.0
   set goal.depth with 5.0
   set goal.speed with 2.5
   go next if elapsed >= goal

frame eastleg in depthmax
   set goal.heading with 90.0
   go next if elapsed >= goal

frame southleg in depthmax
   set goal.heading with 180.0
   go next if elapsed >= goal

frame westleg in depthmax
   set goal.heading with 270.0
   go next if elapsed >= goal

frame mission_stop
   bid stop vehiclesim
   bid stop autopilot
   bid stop logger
   bid stop me

frame abort
   set goal.depth with 0.0
   set goal.speed with 2.5
   go mission_stop if state.depth == 0.0 +- 0.25

framer autopilot be active first autopilot_run

frame autopilot_run
   do controller pid speed
   do controller pid heading
   do controller pid depth
   do controller pid pitch


logger logger to /tmp/log/ioflo/  # /tmp deleted on reboot but /var/tmp not
  log state on update
    loggee state.position as position state.heading as heading \
           state.depth as depth state.speed as  speed

  log goal on update
    loggee goal.heading as heading goal.depth as depth goal.speed as speed

  log frame on update
    loggee framer.mission.state.active as active framer.mission.state.human as human
