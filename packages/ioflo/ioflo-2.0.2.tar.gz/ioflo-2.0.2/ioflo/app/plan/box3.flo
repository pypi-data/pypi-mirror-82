#example mission box3.flo
#with auxiliary for gps fix

house box3

init max.depth with 50.0

framer vehiclesim be active first vehicle_run

frame vehicle_run
   do simulator motion uuv

framer mission be active first northleg

frame depthmax
   go abort if state.depth >= .max.depth

frame northleg in depthmax
   set elapsed with 20.0
   set goal.heading with 0.0
   set goal.depth with 5.0
   set goal.speed with 2.5
   go next if elapsed >= goal

frame gpsfixnorth
   aux gps
   go next if gps is done

frame eastleg in depthmax
   set elapsed with 20.0
   set goal.heading with 90.0
   set goal.depth with 5.0
   set goal.speed with 2.5
   go next if elapsed >= goal

frame gpxfixeast
   aux gps
   go next if gps is done

frame southleg in depthmax
   set elapsed with 20.0
   set goal.heading with 180.0
   set goal.depth with 5.0
   set goal.speed with 2.5
   go next if elapsed >= goal

frame gpxfixsouth
   aux gps
   go next if gps is done

frame westleg in depthmax
   set elapsed with 20.0
   set goal.heading with 270.0
   set goal.depth with 5.0
   set goal.speed with 2.5
   go next if elapsed >= goal

frame gpxfixwest
   aux gps
   go next if gps is done

frame mission_stop
   bid stop vehiclesim
   bid stop autopilot
   bid stop logger
   bid stop me

frame abort
   set goal.depth with 0.0
   set goal.speed with 2.5
   go mission_stop if state.depth == 0.0 +- 0.25

framer autopilot be active first autopilot_run

frame autopilot_run
   do controller pid speed
   do controller pid heading
   do controller pid depth
   do controller pid pitch

logger logger to /tmp/log/ioflo/  # /tmp deleted on reboot but /var/tmp not
  log state on update
    loggee state.position state.heading state.depth state.speed

  log goal on update
    loggee goal.heading goal.depth goal.speed

framer gps be aux first gps1

   frame gps1
      set goal.depth with 0.0
      go next if state.depth == 0.0 +- 0.25

   frame gps2
      set goal.speed with 0.0
      go next if elapsed >= 10.0

   frame gps3
      done


