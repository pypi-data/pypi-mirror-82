# testSlave.flo

house test_slave

  init meta.test with value "test"
  init meta.spot with when "now" where "here"
  init meta.stuff with value "test2"

  framer testmeta be active first doslave1
    frame timeout
      go abort if elapsed >= 5.0

      frame doslave1 in timeout
        ready testslave #benter
        print Slave until done
        start testslave # enter
        run testslave # recur
        go next if testslave is done
        stop testslave # exit

      frame gap in timeout  # need exit above not precluded by benter
        go next

      frame doslave2 in timeout
        ready testslave #benter
        go next if testslave is readied

      frame doslave3 in timeout
        start testslave # enter
        go next if not testslave is readied
                and testslave is started

      frame doslave4 in timeout
        run testslave # recur
        go next if not testslave is readied
                and not testslave is started
                and testslave is running
        stop testslave # exit

      frame doslave5 in timeout
        go next if testslave is stopped

      frame abortslave in timeout
        print abortslave
        abort testslave
        go next if testslave is aborted


    frame done
      print "Success"
      bid stop all

    frame abort
      print "Failure"
      put "Test Slave" into meta.failure
      bid stop all


  framer testslave be slave first slave1 # slave framer
    frame slave1
      print slave1
      go next

    frame slave2
      print slave2
      repeat 3
      recur
        print slave2 again

    frame slave3 #so complete
      print slave3
      done
