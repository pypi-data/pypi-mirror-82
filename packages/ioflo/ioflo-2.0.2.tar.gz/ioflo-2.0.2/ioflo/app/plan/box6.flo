# example mission box6.flo
# uses conditional auxiliary

house box6

init limit.depth.max with 50.0
init limit.depth.shallow with 10.0
init limit.depth.cruise with 5.0

init boxer.heading with 0.0
init boxer.turn with 90.0
init boxer.leg with 0.0
init boxer.nlegs with 4.0

framer vehiclesim be active first vehicle_run

  frame vehicle_run
    do simulator motion uuv

framer mission be active first leg

  frame depthmax
    go abort if state.depth >= .limit.depth.max

    frame shallow in depthmax
      aux shallow if state.depth >= .limit.depth.shallow # until aux done

      frame leg in shallow
        set elapsed with 60.0
        set goal.heading from boxer.heading
        set goal.depth with 15.0
        set goal.speed with 2.5
        go gpsfix if elapsed >= goal
        go mission_stop if .boxer.leg >= .boxer.nlegs

  frame gpsfix
    aux gps
    go leg if gps is done
    exit
      inc boxer.heading from boxer.turn
      inc boxer.leg with 1.0

  frame abort
    set goal.depth with 0.0
    set goal.speed with 2.5
    go mission_stop if depth == 0.0 +- 0.25

  frame mission_stop
    bid stop all

framer autopilot be active first autopilot_run

  frame autopilot_run
    do controller pid speed
    do controller pid heading
    do controller pid depth
    do controller pid pitch

framer shallow be aux first shallow1 # conditional auxiliary

  frame shallow1
    set goal.depth with 0.0
    set goal.speed with 2.5
    go next if state.depth <= .limit.depth.cruise #repaired

  frame shallow2 #so complete
    let me if state.depth <= .limit.depth.cruise # dumb beact
    done

logger logger to /tmp/log/ioflo/  # /tmp deleted on reboot but /var/tmp not
  log state  on update
    loggee state.position state.heading state.depth state.speed

  log goal on update
    loggee goal.heading goal.depth goal.speed goal.duration

load gps.flo
