#example mission box4.flo
# with refactored leg iteration using counter

house box4

init max.depth with 50.0

init boxer.heading with 0.0
init boxer.turn with 90.0
init boxer.leg with 0.0
init boxer.nlegs with 4.0

framer vehiclesim be active first vehicle_run

frame vehicle_run
   do simulator motion uuv

framer mission be active first leg

frame depthmax
   go abort if state.depth >= .max.depth

frame leg in depthmax
   set elapsed with 20.0
   set goal.heading from boxer.heading
   set goal.depth with 5.0
   set goal.speed with 2.5
   go next if elapsed >= goal
   go mission_stop if .boxer.leg >= .boxer.nlegs

frame gpsfix
   aux gps
   go next if gps is done

frame turn in depthmax
   inc boxer.heading from boxer.turn
   inc boxer.leg with 1.0
   go leg

frame abort
   set goal.depth with 0.0
   set goal.speed with 2.5
   go mission_stop if state.depth == 0.0 +- 0.25

frame mission_stop
   bid stop all

framer autopilot be active first autopilot_run

frame autopilot_run
   do controller pid speed
   do controller pid heading
   do controller pid depth
   do controller pid pitch

logger logger to /tmp/log/ioflo/  # /tmp deleted on reboot but /var/tmp not
  log state on update
    loggee state.position state.heading state.depth state.speed

  log goal on update
    loggee goal.heading goal.depth goal.speed

framer gps be aux first gps1

frame gps1
   set goal.depth with 0.0
   go next if state.depth == 0.0 +- 0.25

frame gps2
   set goal.speed with 0.0
   go next if elapsed >= 10.0

frame gps3
   done

